buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'kr.entree.spigradle' version '2.2.3'
    id 'java-library'
    id 'jacoco'
    id 'io.ebean' version '12.6.6'
    id 'idea'
    id "io.freefair.lombok" version "5.3.0"
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 8
targetCompatibility = 8

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    //    depends = ['']
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()

    maven { url 'https://jitpack.io' }
}

dependencies {
    annotationProcessor "io.ebean:querybean-generator:12.6.6"

    compileOnly spigot(mcVersion)

    compile group: 'commons-io', name: 'commons-io', version: '2.8.0'

    api "io.ebean:ebean:12.6.5"
    api 'io.ebean:ebean-querybean:12.6.5'
    implementation "io.ebean:ebean-migration:12.4.0"

    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'org.assertj:assertj-core:3.+'
    testImplementation 'io.ebean:ebean-test:12.6.4'
}

shadowJar {
    classifier = ''
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = true
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'